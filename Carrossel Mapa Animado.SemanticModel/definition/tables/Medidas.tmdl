table Medidas
	lineageTag: 172e4b96-124f-465e-b37b-55d09d9db9ed

	measure Faturamento = ```
			
			    SUMX(
			        fVendas,
			        [Valor Unitario] * [Qtde Vendida]
			    )
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 1. Principais
		lineageTag: 39eb0064-9e3e-4746-845b-b582d855a382

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Custo = ```
			
			    SUMX(
			        fVendas,
			        [Custo Unitario] * [Qtde Vendida]
			    )
			
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 1. Principais
		lineageTag: f7e6e3a5-a9dd-483e-907c-6f3f90682090

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Meta = ```
			
			    SUM( fMeta[Total] ) 
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 1. Principais
		lineageTag: afa1923b-5cc5-4cd7-a8f6-66970ae35b4c

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Lucro = ```
			[Faturamento] - [Custo] 
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 1. Principais
		lineageTag: 98c1b71a-ccd5-4c5d-a123-2604824f7d6d

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Meta Diaria' = ```
			
			-- Diluição de Meta por Dia
			
			var  vMeta_fixa = 
			    CALCULATE( 
			        [Meta],
			        ALL(dCalendario),
			        VALUES( dCalendario[MesId] ),
			        VALUES( dCalendario[Ano])
			    )
			
			VAR vQtde_dias = 
			    CALCULATE( 
			        COUNTROWS(dCalendario),
			        ALL(dCalendario),
			        VALUES( dCalendario[MesId] ),
			        VALUES( dCalendario[Ano])
			    )
			
			
			VAR vResultado = 
			    IF( 
			        ISFILTERED(dCalendario[Dia]) || ISFILTERED(dCalendario[SemanaAno]),
			        DIVIDE( vMeta_fixa, vQtde_dias),
			        [Meta] 
			    )
			
			RETURN 
			    vResultado     
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 1. Principais
		lineageTag: b82d3479-4e4e-4949-917c-70fc65954ca6

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Calendario SVG' = ```
			
			
			VAR vDia = SELECTEDVALUE( dCalendario[Dia] )
			
			VAR vPorcentagem = 
			    [Porcentagem Meta] 
			
			
			VAR vPorcentagem_formatada = 
			    FORMAT( vPorcentagem, "0%" )
			
			VAR vValor = [Faturamento] 
			
			VAR vValor_formatado = 
			    SWITCH(
			        TRUE(),
			        vValor >= 1e9, FORMAT( vValor, "R$ #,0,,,.00 Bi" ),
			        vValor >= 1e6, FORMAT( vValor, "R$ #,0,,.00 Mi" ),
			        vValor >= 1e3, FORMAT( vValor, "R$ #,0,.00 K" ),
			        FORMAT( vValor, "R$ #" )
			    )
			
			
			--------------------------------------
			-- BARRA PROGRESSO 
			-------------------------------------
			
			VAR vFator = 70 
			-- tamanho da barra para atingir o 100% 
			
			VAR vTam_max_barra = 90
			-- Valor de corte, para que o progresso não fique colado no elemento do contorno
			
			VAR vProgresso = 
			    ROUND(vFator * vPorcentagem , 0 )
			
			VAR vProgresso_ajustado = 
			    IF(
			        vProgresso <= vTam_max_barra,
			        vProgresso,
			        vTam_max_barra
			    )
			
			
			
			-------------------------------------
			-- LINHA META 
			-------------------------------------
			
			VAR vMeta_desejada = 1
			-- representa 100% em formatado decimal
			
			VAR vDeslocamento_linha_meta  = 
			    ROUND(vFator * vMeta_desejada  , 0 ) + 5
			
			
			
			-------------------------------------
			-- OPACIDADE
			-------------------------------------
			
			VAR vDestaque_escolhido = SELECTEDVALUE( zVisaoCalendario[Escolha] )
			
			VAR vOpacidade = 
			    SWITCH(
			        TRUE(),
			        ISBLANK( vValor ), "0.2",
			        vDestaque_escolhido = "Completo", "1",
			        vDestaque_escolhido = "Acima meta" && vPorcentagem >= vMeta_desejada, "1",
			         vDestaque_escolhido = "Abaixo meta" && vPorcentagem < vMeta_desejada, "1",
			        "0.2"
			    )
			
			
			-------------------------------------------
			-- PAINEL DE FORMATAÇÃO  
			-------------------------------------------
			
			VAR vBarra_progresso_cor = 
			    IF( vPorcentagem >= vMeta_desejada, "#4A5664", "#D16961" )
			
			
			VAR vBarra_fundo_cor = 
			    vBarra_progresso_cor 
			
			VAR vBarra_fundo_opacidade = "0.3"
			-- Tem que ser formatada como texto, ou seja, constar dentro de aspas o valor
			
			VAR vLinha_meta_cor = "#000"
			VAR vLinha_meta_largura = 2
			
			
			VAR vFonte = "Segoe UI"
			
			VAR vDia_cor = "#393D46"
			VAR vDia_tam = 20
			VAR vDia_peso = 600
			
			VAR vValor_cor = "#393D46"
			VAR vValor_tam = 13
			VAR vValor_peso = 650
			
			
			VAR vRot_categoria = "Meta"
			VAR vRot_cat_texto_cor = "#393D46"
			VAR vRot_cat_texto_tam = 12
			VAR vRot_cat_texto_peso = 600
			
			VAR vRot_cat_porc_cor = "#393D46"
			VAR vRot_cat_porc_tam = 13
			VAR vRot_cat_porc_peso = 650
			
			
			------------------------------------------
			
			VAR vPrefixo = "data:image/svg+xml, "
			
			RETURN 
			
			vPrefixo & 
			
			"
			<svg width='100' height='100' viewBox='0 0 100 100' fill='none' xmlns='http://www.w3.org/2000/svg'>
				
			    <defs>
			        <style>
			            svg{
			                opacity: " & vOpacidade & "
			            }
			
			
			        </style>
			    </defs>
			    
			    <g id='Calendario' clip-path='url(#clip0_4455_151)'>
					
			        
			        <rect id='contorno' x='0.5' y='0.5' width='99' height='99' rx='4.5' stroke='#CBCBCB'/>
					
			        
			        <text id='rot_dia' text-anchor='end'  fill='" & vDia_cor & "'  font-family='" & vFonte & "' font-size='" & vDia_tam & "' font-weight='" & vDia_peso & "' >
						<tspan x='95' y='28'>    " & vDia & "    </tspan>
					</text>
					
			        
			        <text id='rot_valor' text-anchor='middle' fill='" & vValor_cor & "'  font-family='" & vFonte & "' font-size='" & vValor_tam & "' font-weight='" & vValor_peso & "' >
						<tspan x='50' y='53.8636'>        " & vValor_formatado  & "    </tspan>
					</text>
					
			        
			        <text id='rot_cat' fill='" & vRot_cat_texto_cor & "'  font-family='" & vFonte & "' font-size='" & vRot_cat_texto_tam & "' font-weight='" & vRot_cat_texto_peso & "' >
						<tspan x='5' y='77.6364'>        " & vRot_categoria & "   </tspan>
					</text>
					
			        
			        <text id='rot_meta_perc' text-anchor='end' fill='" & vRot_cat_porc_cor & "'  font-family='" & vFonte & "' font-size='" & vRot_cat_porc_tam & "' font-weight='" & vRot_cat_porc_peso & "' >
						<tspan x='95' y='77.8636'>       " & vPorcentagem_formatada  & "     </tspan>
					</text>
					
			        
			        <rect id='barra_fundo' x='5' y='86' width='90' height='7' opacity='" & vBarra_fundo_opacidade & "' fill='" & vBarra_fundo_cor & "'/>
					
			        
			        <rect id='barra_progresso' x='5' y='86' width='" & vProgresso_ajustado & "' height='7' fill='" & vBarra_progresso_cor & "'/>
					
			        
			        <rect id='linha_meta' x='" & vDeslocamento_linha_meta & "' y='82' width='" & vLinha_meta_largura & "' height='14' fill='" & vLinha_meta_cor & "'/>
				
			    
			    </g>
			</svg>
			
			"
			```
		displayFolder: 3. Customizados\Calendario
		lineageTag: e0a9d9c8-3f15-4a90-af70-de3e56c3cecc
		dataCategory: ImageUrl

	measure 'Porcentagem Meta' = ```
			
			    DIVIDE( [Faturamento], [Meta Diaria] ) 
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 1. Principais
		lineageTag: 9d64a045-1556-4273-b540-69a271475a24

	measure 'Faturamento LY' = ```
			
			-- LY = Last Year 
			-- Valor referente ao mesmo período do Ano Anterior
			
			    CALCULATE(
			        [Faturamento],
			        CALCULATETABLE(
			            SAMEPERIODLASTYEAR( dCalendario[Data] ),
			            dCalendario[DataComVenda] = TRUE()
			        )
			    )
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 2. Temporais\Faturamento
		lineageTag: cf178744-17e8-47cd-91a8-c74af5843436

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Faturamento YoY' =
			
			
			VAR vValorAtual = [Faturamento]
			
			VAR vValorAnoAnterior = [Faturamento LY]
			
			RETURN
			    DIVIDE(
			        vValorAtual - vValorAnoAnterior,
			        vValorAnoAnterior
			    )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 2. Temporais\Faturamento
		lineageTag: 0859f2c6-051e-4791-b949-823bb386f22c

	measure 'Faturamento LM' = ```
			
			-- LM = Last Month 
			-- Valor referente ao mesmo período do Mês Anterior
			
			    CALCULATE(
			        [Faturamento],
			        CALCULATETABLE(
			            DATEADD( dCalendario[Data], -1, MONTH ),
			            dCalendario[DataComVenda] = TRUE()
			        )
			    )
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 2. Temporais\Faturamento
		lineageTag: 5dd1f1f7-a4d2-48b0-a05f-9062184968c9

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Faturamento MoM' =
			
			
			VAR vValorAtual = [Faturamento]
			
			VAR vValorMesAnterior = [Faturamento LM]
			
			RETURN
			    DIVIDE(
			        vValorAtual - vValorMesAnterior,
			        vValorMesAnterior
			    )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 2. Temporais\Faturamento
		lineageTag: 743e5615-c959-4650-87bc-927723befeee

	measure 'Custo LM' = ```
			
			-- LM = Last Month 
			-- Valor referente ao mesmo período do Mês Anterior
			
			    CALCULATE(
			        [Custo],
			        CALCULATETABLE(
			            DATEADD( dCalendario[Data], -1, MONTH ),
			            dCalendario[DataComVenda] = TRUE()
			        )
			    )
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 2. Temporais\Custo
		lineageTag: cb4e6818-030d-4be0-8a0d-4145bc6c1d81

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Custo MoM' =
			
			
			VAR vValorAtual = [Custo]
			
			VAR vValorMesAnterior = [Custo LM]
			
			RETURN
			    DIVIDE(
			        vValorAtual - vValorMesAnterior,
			        vValorMesAnterior
			    )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 2. Temporais\Custo
		lineageTag: efdc69e4-a32f-400e-abd1-e48e24f0bb90

	measure 'Custo LY' = ```
			
			-- LY = Last Year 
			-- Valor referente ao mesmo período do Ano Anterior
			
			    CALCULATE(
			        [Custo],
			        CALCULATETABLE(
			            SAMEPERIODLASTYEAR( dCalendario[Data] ),
			            dCalendario[DataComVenda] = TRUE()
			        )
			    )
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 2. Temporais\Custo
		lineageTag: c586df81-daf9-4303-a8ba-f408398d2ebf

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Custo YoY' =
			
			
			VAR vValorAtual = [Custo]
			
			VAR vValorAnoAnterior = [Custo LY]
			
			RETURN
			    DIVIDE(
			        vValorAtual - vValorAnoAnterior,
			        vValorAnoAnterior
			    )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 2. Temporais\Custo
		lineageTag: d20749d7-858c-4c58-a2d3-3e1c2e67c9cc

	measure 'Lucro LY' = ```
			
			-- LY = Last Year 
			-- Valor referente ao mesmo período do Ano Anterior
			
			    CALCULATE(
			        [Lucro],
			        CALCULATETABLE(
			            SAMEPERIODLASTYEAR( dCalendario[Data] ),
			            dCalendario[DataComVenda] = TRUE()
			        )
			    )
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 2. Temporais\Lucro
		lineageTag: c0e806d1-dea7-4b36-a7c7-66262fb385a9

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Lucro YoY' =
			
			
			VAR vValorAtual = [Lucro]
			
			VAR vValorAnoAnterior = [Lucro LY]
			
			RETURN
			    DIVIDE(
			        vValorAtual - vValorAnoAnterior,
			        vValorAnoAnterior
			    )
		formatString: #,0.00%;-#,0.00%;#,0.00%
		displayFolder: 2. Temporais\Lucro
		lineageTag: 82dafeb7-0518-404b-b0a6-8f5da7bc15dd

	measure 'Lucro LM' = ```
			
			-- LM = Last Month 
			-- Valor referente ao mesmo período do Mês Anterior
			
			    CALCULATE(
			        [Lucro],
			        CALCULATETABLE(
			            DATEADD( dCalendario[Data], -1, MONTH ),
			            dCalendario[DataComVenda] = TRUE()
			        )
			    )
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: 2. Temporais\Lucro
		lineageTag: 323ca628-0eae-43f6-b00b-e4aaf9ca371d

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Lucro MoM' =
			
			
			VAR vValorAtual = [Lucro]
			
			VAR vValorMesAnterior = [Lucro LM]
			
			RETURN
			    DIVIDE(
			        vValorAtual - vValorMesAnterior,
			        vValorMesAnterior
			    )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 2. Temporais\Lucro
		lineageTag: 1bbc7edc-aa14-4848-b4a9-df00165c6a7a

	measure Carrossel = ```
			
			
			VAR vImgBrasil = "https://i.imgur.com/hZPx2rc.png"
			VAR vImgNorte = "https://i.imgur.com/CARGzFT.png"
			VAR vImgNordeste = "https://i.imgur.com/HyrWVNa.png"
			VAR vImgCentroOeste = "https://i.imgur.com/1GIklFu.png"
			VAR vImgSudeste = "https://i.imgur.com/87yfOtS.png"
			VAR vImgSul = "https://i.imgur.com/PwUnQuT.png"
			
			VAR vRegiaoSelecionada = SELECTEDVALUE(dClientes[Regiao], "Brasil")
			
			VAR vSul = 
			"
			    <image href="&vImgBrasil&" id='posicao_1' x='195' y='71' width='190' height='190'/>		
			    <image href="&vImgNorte&" id='posicao_2' x='470' y='218' width='60' height='60'/>		
			    <image href="&vImgNordeste&" id='posicao_3' x='365' y='278' width='60' height='60'/>		
			    <image href="&vImgCentroOeste&" id='posicao_4' x='260' y='308' width='60' height='60'/>		
			    <image href="&vImgSudeste&" id='posicao_5' x='155' y='278' width='60' height='60'/>		
			    <image href="&vImgSul&" id='posicao_6' x='50' y='218' width='60' height='60'/>	
			"
			
			VAR vSudeste = 
			"
			    <image href="&vImgSul&" id='posicao_1' x='195' y='71' width='190' height='190'/>		
			    <image href="&vImgBrasil&" id='posicao_2' x='470' y='218' width='60' height='60'/>		
			    <image href="&vImgNorte&" id='posicao_3' x='365' y='278' width='60' height='60'/>		
			    <image href="&vImgNordeste&" id='posicao_4' x='260' y='308' width='60' height='60'/>		
			    <image href="&vImgCentroOeste&" id='posicao_5' x='155' y='278' width='60' height='60'/>		
			    <image href="&vImgSudeste&" id='posicao_6' x='50' y='218' width='60' height='60'/>	
			"
			
			VAR vCentroOeste = 
			"
			    <image href="&vImgSudeste&" id='posicao_1' x='195' y='71' width='190' height='190'/>		
			    <image href="&vImgSul&" id='posicao_2' x='470' y='218' width='60' height='60'/>		
			    <image href="&vImgBrasil&" id='posicao_3' x='365' y='278' width='60' height='60'/>		
			    <image href="&vImgNorte&" id='posicao_4' x='260' y='308' width='60' height='60'/>		
			    <image href="&vImgNordeste&" id='posicao_5' x='155' y='278' width='60' height='60'/>		
			    <image href="&vImgCentroOeste&" id='posicao_6' x='50' y='218' width='60' height='60'/>	
			"
			
			VAR vNordeste = 
			"
			    <image href="&vImgCentroOeste&" id='posicao_1' x='195' y='71' width='190' height='190'/>		
			    <image href="&vImgSudeste&" id='posicao_2' x='470' y='218' width='60' height='60'/>		
			    <image href="&vImgSul&" id='posicao_3' x='365' y='278' width='60' height='60'/>		
			    <image href="&vImgBrasil&" id='posicao_4' x='260' y='308' width='60' height='60'/>		
			    <image href="&vImgNorte&" id='posicao_5' x='155' y='278' width='60' height='60'/>		
			    <image href="&vImgNordeste&" id='posicao_6' x='50' y='218' width='60' height='60'/>	
			"
			VAR vNorte = 
			"
			    <image href="&vImgNordeste&" id='posicao_1' x='195' y='71' width='190' height='190'/>		
			    <image href="&vImgCentroOeste&" id='posicao_2' x='470' y='218' width='60' height='60'/>		
			    <image href="&vImgSudeste&" id='posicao_3' x='365' y='278' width='60' height='60'/>		
			    <image href="&vImgSul&" id='posicao_4' x='260' y='308' width='60' height='60'/>		
			    <image href="&vImgBrasil&" id='posicao_5' x='155' y='278' width='60' height='60'/>		
			    <image href="&vImgNorte&" id='posicao_6' x='50' y='218' width='60' height='60'/>	
			"
			
			VAR vBrasil = 
			"
			    <image href="&vImgNorte&" id='posicao_1' x='195' y='71' width='190' height='190'/>		
			    <image href="&vImgNordeste&" id='posicao_2' x='470' y='218' width='60' height='60'/>		
			    <image href="&vImgCentroOeste&" id='posicao_3' x='365' y='278' width='60' height='60'/>		
			    <image href="&vImgSudeste&" id='posicao_4' x='260' y='308' width='60' height='60'/>		
			    <image href="&vImgSul&" id='posicao_5' x='155' y='278' width='60' height='60'/>		
			    <image href="&vImgBrasil&" id='posicao_6' x='50' y='218' width='60' height='60'/>	
			"
			
			RETURN
			
			"
			<svg viewBox='0 0 580 385' fill='none' xmlns='http://www.w3.org/2000/svg'>
			
			    <style>
			        #posicao_1{animation: anima1_para2 1s linear forwards}
			        #posicao_2{animation: anima2_para3 1s linear forwards}
			        #posicao_3{animation: anima3_para4 1s linear forwards}
			        #posicao_4{animation: anima4_para5 1s linear forwards}
			        #posicao_5{animation: anima5_para6 1s linear forwards}
			        #posicao_6{animation: anima6_para1 1s linear forwards}
			
			        @keyframes anima1_para2{
			            to {x: 470; y: 218; width: 60px; height: 60px}
			        }
			
			        @keyframes anima2_para3{
			            to {x: 365; y: 278; width: 60px; height: 60px}
			        }
			
			        @keyframes anima3_para4{
			            to {x: 260; y: 308; width: 60px; height: 60px}
			        }
			
			        @keyframes anima4_para5{
			            to {x: 155; y: 278; width: 60px; height: 60px}
			        }
			
			        @keyframes anima5_para6{
			            to {x: 50; y: 218; width: 60px; height: 60px}
			        }
			
			        @keyframes anima6_para1{
			            to {x: 195; y: 71; width: 190px; height: 190px}
			        }
			
			        #nome{
			            transform: translateX(-400px);
			            animation: animaTexto 1s linear forwards
			        }
			
			        @keyframes animaTexto{
			            to {transform: translateX(0px)}
			        }
			
			    </style>
				
			    <g id='strutura_carrossel'>
			
			        "&
			            SWITCH(
			                vRegiaoSelecionada,
			                "Sul", vSul,
			                "Sudeste", vSudeste,
			                "Centro Oeste", vCentroOeste,
			                "Nordeste", vNordeste,
			                "Norte", vNorte,
			                vBrasil
			            )
			        &"
					
			        <text id='nome' text-anchor='middle' fill='#4a5664' font-family='Segoe UI' font-size='30'>
						
			            <tspan x='290' y='41.8636'>"&vRegiaoSelecionada&"</tspan>
					
			        </text>
					
			        
			    </g>
			
			</svg>
			"
			```
		lineageTag: de41a1e8-6204-4b5c-92be-ec9d459ead16

	measure 'Cor Invisivel' =
			
			
			"#0000"
		lineageTag: a49000a9-2214-442f-88bf-81c5539c041f

	column Value
		isHidden
		formatString: 0
		lineageTag: 51226410-d079-432a-98eb-080500c290ce
		summarizeBy: sum
		isNameInferred
		sourceColumn: [Value]

		annotation SummarizationSetBy = Automatic

	partition Medidas = calculated
		mode: import
		source = {0}

	annotation PBI_Id = 0a7398d85d994e92850025ca944768cf

